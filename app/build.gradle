plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jlleitschuh.gradle.ktlint' version '11.3.1'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'androidx.navigation.safeargs.kotlin'
}

def packageName = "org.linphone"

android {
    namespace 'org.linphone'
    compileSdk 34

    defaultConfig {
        applicationId packageName
        minSdk 27
        targetSdk 34
        versionCode 60000
        versionName "6.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            resValue "string", "file_provider", packageName + ".fileprovider"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "file_provider", packageName + ".fileprovider"
        }
    }
    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation "androidx.slidingpanelayout:slidingpanelayout:1.2.0"

    def nav_version = "2.7.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def emoji_version = "1.4.0"
    implementation "androidx.emoji2:emoji2:$emoji_version"
    implementation "androidx.emoji2:emoji2-emojipicker:$emoji_version"

    // https://github.com/material-components/material-components-android/blob/master/LICENSE Apache v2.0
    implementation 'com.google.android.material:material:1.9.0'

    // https://github.com/coil-kt/coil/blob/main/LICENSE.txt Apache v2.0
    def coil_version = "2.4.0"
    implementation("io.coil-kt:coil:$coil_version")
    implementation("io.coil-kt:coil-gif:$coil_version")
    implementation("io.coil-kt:coil-svg:$coil_version")
    implementation("io.coil-kt:coil-video:$coil_version")
    // https://github.com/GetStream/avatarview-android/blob/main/LICENSE Apache v2.0
    implementation "io.getstream:avatarview-coil:1.0.7"

    implementation platform('com.google.firebase:firebase-bom:30.3.2')
    implementation 'com.google.firebase:firebase-messaging'

    implementation 'org.linphone:linphone-sdk-android:5.3+'
}

ktlint {
    android = true
    ignoreFailures = true
}

project.tasks['preBuild'].dependsOn 'ktlintFormat'